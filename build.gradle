buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    group = 'org.strangeway'
    version = '0.1'

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'application'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.2'

    compile 'org.eclipse.jetty:jetty-server:9.4.0.v20161208'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.0.v20161208'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.4.0.v20161208'

    compile 'com.vaadin:vaadin-server:7.7.6'
    compile 'com.vaadin:vaadin-push:7.7.6'
    compile 'com.vaadin:vaadin-client-compiled:7.7.6'
    compile 'com.vaadin:vaadin-themes:7.7.6'
}

applicationName = 'electron-vaadin'
mainClassName = 'org.strangeway.electronvaadin.Launcher'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations.archives.artifacts.removeAll {
    it.file =~ 'tar'
}

// Prepare files for serving from file system using vaadin:// protocol
task bundle(group: 'build', dependsOn: installDist) {
    outputs.dir(project.file('build/install/electron-vaadin/VAADIN'))

    doLast {
        def compileFiles = project.configurations.getByName('compile').resolve()
        for (f in compileFiles) {
            copy{
                includeEmptyDirs = false
                from(zipTree(f))
                into(project.file('build/install/electron-vaadin'))
                include 'VAADIN/**'
                exclude 'VAADIN/widgetsets/WEB-INF/**'
                exclude 'VAADIN/themes/chameleon/**'
                exclude 'VAADIN/themes/liferay/**'
                exclude 'VAADIN/themes/reindeer/**'
                exclude 'VAADIN/themes/runo/**'
                exclude 'VAADIN/themes/valo/fonts/lato/**'
                exclude 'VAADIN/themes/valo/fonts/lora/**'
                exclude 'VAADIN/themes/valo/fonts/roboto/**'
                exclude 'VAADIN/themes/valo/fonts/source-sans-pro/**'
                exclude 'VAADIN/**/*.gz'
                exclude 'VAADIN/**/*.scss'
                exclude 'VAADIN/**/readme.txt'
            }
        }
    }
}